buildscript {
   repositories {
      mavenCentral()
   }
   dependencies {
      classpath 'com.android.tools.build:gradle:0.8.0'
   }
}
apply plugin: 'android'

repositories {
   mavenCentral()
   mavenLocal() // Needed to pick up analytics from maven-android-sdk-deployer
   maven {
      url 'https://raw.github.com/iFixit/ark/master/releases/'
   }
}

dependencies {
   compile fileTree(dir: 'libs', include: '*.jar')
   compile "com.android.support:support-v4:18.0.0"
   compile "com.actionbarsherlock:actionbarsherlock:4.4.0@aar"
   compile "com.squareup.okhttp:okhttp:1.2.1"
   compile "com.marczych.androidsectionheaders:androidsectionheaders:1.0.0"
   compile "com.squareup:otto:1.3.4"
   compile "com.github.kevinsawicki:http-request:5.4.1"
   compile "org.jsoup:jsoup:1.7.2"
   compile "com.google.code.gson:gson:2.2.4"
   compile "com.squareup.picasso:picasso:2.0.2"
   compile "net.simonvt.menudrawer:menudrawer:3.0.4@aar"
   compile "com.viewpagerindicator:viewpagerindicator:2.4.1"
   compile "com.github.chrisbanes.pulltorefresh:pulltorefresh:2.1.2"
   compile "it.sephiroth.android.library.imagezoom:imagezoom:1.0.0"
   compile "com.mobeta.android.dslv:dslv:0.6.1"
}

// List of sites used to create signingConfigs, sourceSets, and productFlavors.
def sites = ["ifixit", "dozuki", "accustream"]

android {
   compileSdkVersion 19
   buildToolsVersion "19.0.1"

   defaultConfig {
      versionCode 45
      versionName "2.4.0"
      minSdkVersion 8
      targetSdkVersion 19
      packageName "com.dozuki.ifixit"
   }

   signingConfigs {
      for (site in sites) {
         "${site}" createSiteSigningConfig(site)
      }
   }

   buildTypes {
      debug {
         buildConfigField "String", "DEV_SERVER", '"' + devServer + '"'
      }

      release {
         buildConfigField "String", "DEV_SERVER", '""'

         runProguard true
         proguardFile 'proguard.cfg'
      }
   }

   lintOptions {
      /* Check release builds but don't abort on error. */
      checkReleaseBuilds true
      abortOnError false
   }

   /* Construct base product flavors. */
   productFlavors {
      for (site in sites) {
         "${site}" {
            signingConfig signingConfigs."${site}"
            packageName "com.dozuki.${site}"

            buildConfigField "String", "SEARCH_PROVIDER_AUTHORITY", "\"com.dozuki.${site}.ui.search.SearchSuggestionProvider\""
            buildConfigField "String", "SITE_NAME", '"' + site + '"'
            buildConfigField "String", "APP_ID", '"' + getProperty("${site}AppId") + '"'
            buildConfigField "String", "GA_PROPERTY_ID", '"' + getProperty("${site}GAPropertyId") + '"'
         }
      }
   }

   /* Add site-specific tweaks. */

   productFlavors.dozuki.ext {
      dependencies {
         accustreamCompile 'com.embarkmobile:zxing-android-minimal:1.1.4@aar'
         accustreamCompile 'com.google.zxing:core:2.2'
      }

      minSdkVersion = 9
   }

   productFlavors.accustream.ext {
      dependencies {
         accustreamCompile 'com.embarkmobile:zxing-android-minimal:1.1.4@aar'
         accustreamCompile 'com.google.zxing:core:2.2'
      }

      minSdkVersion = 9
   }

   sourceSets {
      main {
         manifest.srcFile 'AndroidManifest.xml'
         java.srcDirs = ['src']
         resources.srcDirs = ['src']
         aidl.srcDirs = ['src']
         renderscript.srcDirs = ['src']
         res.srcDirs = ['res']
         assets.srcDirs = ['assets']
      }

      for (site in sites) {
         "${site}" createSiteSourceSet(site)
      }
   }
}

/* Various functions to create DSL for a site. */

def createSiteSigningConfig(siteName) {
   return {
      storeFile file("sites/${siteName}/keystore")
      keyAlias getProperty("${siteName}KeyAlias")
      storePassword getProperty("${siteName}StorePassword")
      keyPassword getProperty("${siteName}KeyPassword")
   }
}

def createSiteSourceSet(siteName) {
   if (siteName == 'dozuki') {
      return {
         manifest.srcFile "sites/dozuki/AndroidManifest.xml"
         res.srcDirs = ["sites/dozuki/res", "sites/accustream/res", "sites/ifixit/res"]
         java.srcDirs = ["sites/dozuki/src", "sites/accustream/src", "sites/ifixit/src"]
      }
   } else {
      return {
         manifest.srcFile "sites/${siteName}/AndroidManifest.xml"
         res.srcDirs = ["sites/${siteName}/res", "sites/${siteName}/site-res"]
         java.srcDirs = ["sites/${siteName}/src"]
      }
   }
}
